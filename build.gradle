plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.5'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'org.openapi.generator' version "7.10.0"
}

group = 'bg.thereadingroom'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Test containers
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Openapi
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'

	// Jackson
	implementation 'com.sun.xml.bind:jaxb-impl:2.3.8'

	// openAPI jackson extension (used by code generation)
	implementation "org.openapitools:jackson-databind-nullable:0.2.6"
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('compileJava') {
	dependsOn(tasks.openApiGenerate)
}

//sourceSets.main.java.srcDir "${buildDir}/generate-resources/main/src/main/java"

sourceSets {
	main {
		java {
			srcDirs += "$buildDir/generated/src/main/java"
		}
	}
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/resources/openapi.yml".toString()
	outputDir = "$buildDir/generated".toString()
	templateDir = "$rootDir/openapi".toString()
	apiPackage = "bg.thereadingroom.web"
	modelPackage = "bg.thereadingroom.model.dto"

	enablePostProcessFile = true
	skipOverwrite = false
	modelNameSuffix = "DTO"

	configOptions = [
			delegatePattern               : "true",
			async                         : "false",
			useOptional                   : "true",
			java8                         : "true",
			dateLibrary                   : "java8",
			serializationLibrary          : "jackson",
			library                       : "spring-boot",
			useBeanValidation             : "true",
			interfaceOnly                 : "false",
			serializableModel             : "true",
			withXml                       : "false",
			useTags                       : "true",
			unhandledException            : "true",
			additionalModelTypeAnnotations: "@lombok.Builder(setterPrefix = \"set\")",
			useJakartaEe                  : "true",
			useSpringBoot3                : "true"
	]
}

springBoot {
	mainClass = "bg.thereadingroom.ThereadingroomApplication"
}
